//
// Copyright 2022 Nuna inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

// A virtual type for underlying engines.
// TODO(catalin): neet to support native types.
typedef Engine = Struct<String>

// Creates an empty dataset with specified structure.
def method empty_dataset(
  engine: Engine, seed: {T: Struct}) : Dataset<T> =>
[[skip_conversion]][[end]]
[[pyinline]]${engine}.empty_dataset(${seed})[[end]]


// Reads a CSV file specification and creates a dataset with
// a specified schema determined by T.
// Example: read_csv(Foo(), '/path/to/foo.parquet')
def method read_csv(
  engine: Engine, seed: {T: Struct},
  file_spec: String) : Dataset<T> =>
[[skip_conversion]][[end]]
[[pyinline]]${engine}.read_csv(${seed}, ${file_spec})[[end]]

// Reads a Parquet file specification and creates a
// dataset with specified schema determined by T.
// Example: read_parquet(Foo(), '/path/to/foo.parquet')
def method read_parquet(
  engine: Engine, seed: {T: Struct},
  file_spec: String) : Dataset<T> =>
[[skip_conversion]][[end]]
[[pyinline]]${engine}.read_parquet(${seed}, ${file_spec})[[end]]

// Applies the provided function on all members of src dataset,
// obtaining a dataset from the results.
def method map(src: Dataset<{T: Any}>,
               f: Function<T, {Y: Struct}>) : Dataset<Y> =>
[[skip_conversion]][[end]]
[[pyinline]]${src}.dataset_map(${{dataset_seed}}, ${f})[[end]]

// Applies the provided function that returns an array, then
// flattens it into the output. E.g. if you return this
// sequence of arrays: [1, 2], [], [3], [4, 5, 6] you would get
// 1,2,3,4,5,6 as output.
def method flat_map(src: Dataset<{T: Any}>,
                    f: Function<T, Array<{Y: Struct}>>) : Dataset<Y> =>
[[skip_conversion]][[end]]
[[pyinline]]${src}.dataset_flat_map(${{dataset_seed}}, ${f})[[end]]

// Filters the input dataset by the provided predicate `f`.
def method filter(src: Dataset<{T: Any}>,
                  f: Function<T, Bool>) : Dataset<T> =>
[[skip_conversion]][[end]]
[[pyinline]]${src}.dataset_filter(${f})[[end]]

// Limits the input dataset to a number of elements.
def method limit(src: Dataset<{T: Any}>,
                 size: Int) : Dataset<T> =>
[[skip_conversion]][[end]]
[[pyinline]]${src}.dataset_limit(${size})[[end]]

// Specification for a join. We do only left joins.
//
// Example:
//   Say `foo` is a dataset of
//     Foo = { foo_id: Int; value: String }
//   and `bar` another dataset of
//     Bar = { bar_id: Int; bar_foo_id: Int; info: String }
//
// For a 1:1 relationship between foo_id and bar_foo_id, use:
// foo.join_left(
//   f => f.foo_id,
//   { bar_field = bar.join_by(b => b.bar_foo_id) })
// To obtain dataset of:
//   { foo_id: Int; value: String; bar_field: Nullable<Bar> }
//
// For 1:many relationship between foo_id and bar_foo_id use:
// foo.join_left(
//   f => f.foo_id,
//   { bar_field = bar.join_multi_by(b => b.bar_foo_id) })
// To obtain a dataset of:
//   { foo_id: Int; value: String; bar_field: Array<Bar> }
//
def method join_left(
  left_src: Dataset<{T: Struct}>,
  left_key: {F: Function<T, {K}>},
  right_spec: {Spec: Tuple}
) : DatasetJoin<T, F, Spec> =>
[[skip_conversion]][[end]]
[[pyinline]]${left_src}.dataset_join_left(${{dataset_seed}}, ${left_key}, ${right_spec})[[end]]

def method join_by(
  right_src: {R: Dataset<{T: Struct}>},
  right_key: {F: Function<T, {K}>}
) : Tuple<R, F> => {right = right_src, key = right_key}

def method join_multi_by(
  right_src: {R: Dataset<{T: Struct}>},
  right_key: {F: Function<T, {K}>}
) : Tuple<R, F> => {right_multi = right_src, key = right_key}

def method join_multi_by(
  right_src: {R: Array<Dataset<{T: Struct}>>},
  right_key: {F: Function<T, {K}>}
) : Tuple<R, F> => {right_multi_array = right_src, key = right_key}


//
// Aggregates: we build an aggregation specification as a tuple, then
// pass it to an aggregate dataset function. Examples:
//
// my_dataset.aggregate(
//   s => _agg(s)
//          .group_by({foo = s.foo})
//          .count({num_foos = 1})
//          .sum({total_bar = s.bar})
// )
//
def method aggregate(src: Dataset<{T}>,
  builder: Function<T, Tuple<T, {Spec: Tuple}>>)
    : DatasetAggregate<Spec> =>
[[skip_conversion]][[end]]
[[pyinline]]${src}.dataset_aggregate(${{dataset_seed}}, ${builder})[[end]]

// Root for an aggregation.
// For detail specification of aggregates check aggregate.ndl
def agg(t: {T}): Tuple<T, Tuple<T>> => [t, {_arg = t}]

def method group_by(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{G}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<G>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {group_by = value}) }

def method group_by(t: Tuple<{T}, {A: Tuple}>, value: {G})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<G>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {group_by = { _unnamed = value }}) }

def method sum(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Numeric}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {sum = value}) }

def method sum(t: Tuple<{T}, {A: Tuple}>, value: {V: Numeric})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {sum = { _unnamed = value }}) }

def method min(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Sortable}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {min = value}) }

def method min(t: Tuple<{T}, {A: Tuple}>, value: {V: Sortable})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {min = { _unnamed = value }}) }

def method max(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Sortable}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {max = value}) }

def method max(t: Tuple<{T}, {A: Tuple}>, value: {V: Sortable})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {max = { _unnamed = value }}) }

def method count_distinct(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Any}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {count_distinct = value}) }

def method count_distinct(t: Tuple<{T}, {A: Tuple}>, value: {V: Sortable})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(
    t[1], {count_distinct = { _unnamed = value }}) }

def method count(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{C: Numeric}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<C>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {count = value}) }

def method count(t: Tuple<{T}, {A: Tuple}>, value: {C: Numeric})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<{C}>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {count = { _unnamed = value }}) }

def method mean(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Numeric}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {mean = value}) }

def method mean(t: Tuple<{T}, {A: Tuple}>, value: {V: Numeric})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {mean = { _unnamed = value }}) }

def method to_set(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Any}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {to_set = value}) }

def method to_set(t: Tuple<{T}, {A: Tuple}>, value: {V: Any})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {to_set = { _unnamed = value }}) }

def method to_array(t: Tuple<{T}, {A: Tuple}>, value: Tuple<{V: Any}>)
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {to_array = value}) }

def method to_array(t: Tuple<{T}, {A: Tuple}>, value: {V: Any})
    : Tuple<T, TupleJoin<A, Tuple<Tuple<V>>>> =>
  return { arg = t[0], agg = tuple_join(t[1], {to_array = { _unnamed = value }}) }

// Collects the result of a dataset as an array.
// Of course, it must fit in memory or so :)
// We may choose to limit the collection to say 10k records, but
// still should fit in memory.
def method collect(src: Dataset<{T}>) : Array<T> =>
[[pyinline]]${src}.dataset_collect()[[end]]
